plugins {
  id "org.jetbrains.kotlin.jvm" version "1.9.24"
  id "java-library"
  id "maven-publish"
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
  id "signing"
}

group "com.kotcrab.kmips"
version "1.7-SNAPSHOT"

repositories {
  mavenCentral()
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "11"
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "11"
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.24"
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
  useJUnitPlatform()
}

java {
  withJavadocJar()
  withSourcesJar()
}

nexusPublishing {
  repositories {
    sonatype()
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)
      pom {
        name.set("kmips")
        description.set("MIPS assembler intended for assembling small code patches")
        url.set("https://github.com/kotcrab/kmips")
        licenses {
          license {
            name.set("The Apache License, Version 2.0")
            url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
          }
        }
        developers {
          developer {
            id.set("kotcrab")
            name.set("Kotcrab")
            url.set("https://kotcrab.com")
          }
        }
        scm {
          connection.set("scm:git:git@github.com:kotcrab/kmips.git")
          developerConnection.set("scm:git:git@github.com:kotcrab/kmips.git")
          url.set("git@github.com:kotcrab/kmips.git")
        }
      }
    }
  }
}

signing {
  required { !project.version.endsWith("-SNAPSHOT") && !project.hasProperty("skipSigning") }
  sign publishing.publications.mavenJava
}
